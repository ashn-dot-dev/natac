import "c";
import "std";

import "shared.sunder";

struct server_state {
    var board: board;

    func init() server_state {
        var board = board::init_starter_map_for_beginners();

        return (:server_state){
            .board = board,
        };
    }

    func fini(self: *server_state) void {
        self.*.board.fini();
    }

    func to_server_message(self: *server_state) server_message {
        return server_message::init(&self.*.board);
    }

    func place_road(self: *server_state, road: road) void {
        insert_road(&self.*.board.roads, road);
    }

    func remove_road(self: *server_state, edge: edge) void {
        self.*.board.roads.remove(edge);
    }

    func place_town(self: *server_state, town: town) void {
        insert_town(&self.*.board.towns, town);
    }

    func remove_town(self: *server_state, node: node) void {
        self.*.board.towns.remove(node);
    }

    func update_from_client_message(self: *server_state, message: *client_message) void {
        switch message.*.kind {
        client_message::BUILD_ROAD {
            self.*.place_road(message.*.data.build_road);
        }
        client_message::BUILD_TOWN {
            self.*.place_town(message.*.data.build_town);
        }
        client_message::REMOVE_ROAD {
            self.*.remove_road(message.*.data.remove_road);
        }
        client_message::REMOVE_TOWN {
            self.*.remove_town(message.*.data.remove_town);
        }
        }
    }
}

func server_update(sstate: *server_state, sender: *message_sender[[server_message]]) void {
    var message = sstate.*.to_server_message();
    sender.*.send(&message);
    message.fini();
}
