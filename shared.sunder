import "c";
import "std";

import "bubby";
import "nbnet";

enum player {
    NIL = 0;
    RED = 1;
    BLUE = 2;
    WHITE = 3;
    ORANGE = 4;
}

# Hex position using the cube coordinate system satisfying q + r + s == 0.
struct hex {
    var q: sint;
    var r: sint;
    var s: sint;

    func init(q: sint, r: sint, s: sint) hex {
        assert q + r + s == 0;
        return (:hex){.q = q, .r = r, .s = s};
    }

    func init_round(q_fractional: f32, r_fractional: f32, s_fractional: f32) hex {
        var q_rounded = f32::round(q_fractional);
        var r_rounded = f32::round(r_fractional);
        var s_rounded = f32::round(s_fractional);

        var q_diff = f32::abs(q_rounded - q_fractional);
        var r_diff = f32::abs(r_rounded - r_fractional);
        var s_diff = f32::abs(s_rounded - s_fractional);

        var q = (:sint)q_rounded;
        var r = (:sint)r_rounded;
        var s = (:sint)s_rounded;
        if q_diff > r_diff and q_diff > s_diff {
            q = (:sint)-r_rounded - (:sint)s_rounded;
        }
        elif r_diff > s_diff {
            r = (:sint)-q_rounded - (:sint)s_rounded;
        }
        else {
            s = (:sint)-q_rounded - (:sint)r_rounded;
        }

        return hex::init(q, r, s);
    }

    func hash(self: *hex) usize {
        var h = self.*.q.hash() +% self.*.q.hash() +% self.*.s.hash();
        return h *% h;
    }

    func compare(lhs: *hex, rhs: *hex) ssize {
        var cmp = sint::cmp(lhs.*.q, rhs.*.q);
        if cmp != 0 {
            return cmp;
        }

        var cmp = sint::cmp(lhs.*.r, rhs.*.r);
        if cmp != 0 {
            return cmp;
        }

        var cmp = sint::cmp(lhs.*.s, rhs.*.s);
        if cmp != 0 {
            return cmp;
        }

        return 0;
    }

    func format(self: *hex, writer: std::writer, fmt: []byte) std::result[[void, std::error]] {
        if std::str::eq(fmt, "") {
            return std::write_format(
                writer,
                "(q={}, r={}, s={})",
                (:[]std::formatter)[
                    std::formatter::init[[sint]](&self.*.q),
                    std::formatter::init[[sint]](&self.*.r),
                    std::formatter::init[[sint]](&self.*.s)]);
        }

        return std::result[[void, std::error]]::init_value(void::VALUE);
    }

    func init_from_bubby(value: *bubby::value) hex {
        var q = (:sint)value.*.vector().*.data()[0].integer();
        var r = (:sint)value.*.vector().*.data()[1].integer();
        var s = -q - r;
        return hex::init(q, r, s);
    }

    func to_bubby(self: *hex) bubby::value {
        var out = bubby::value::init_vector();
        out.vector().*.push(bubby::value::init_integer((:s64)self.*.q));
        out.vector().*.push(bubby::value::init_integer((:s64)self.*.r));
        return out;
    }

    func subtract(lhs: hex, rhs: hex) hex {
        return hex::init(lhs.q - rhs.q, lhs.r - rhs.r, lhs.s - rhs.s);
    }

    func distance(a: hex, b: hex) sint {
        var vec = hex::subtract(a, b);
        return (sint::abs(vec.q) + sint::abs(vec.r) + sint::abs(vec.s)) / 2;
    }
}

enum resource {
    BRICK;
    ORE;
    SHEEP;
    WHEAT;
    WOOD;

    func data(self: *resource) []byte {
        switch *self {
        BRICK {
            return "brick";
        }
        ORE {
            return "ore";
        }
        SHEEP {
            return "sheep";
        }
        WHEAT {
            return "wheat";
        }
        WOOD {
            return "wood";
        }
        }

        std::unreachable(fileof(), lineof());
        return std::zeroed[[[]byte]]();
    }

    func cstr(self: *resource) *byte {
        return startof(self.*.data());
    }

    func format(self: *resource, writer: std::writer, fmt: []byte) std::result[[void, std::error]] {
        if std::str::eq(fmt, "") {
            return std::write_all(writer, self.*.data());
        }

        return std::result[[void, std::error]]::init_error(std::error::INVALID_ARGUMENT);
    }
}

struct tile {
    var hex: hex;
    var kind: enum {
        DESERT;
        OCEAN;

        FOREST;
        FIELD;
        HILL;
        MOUNTAIN;
        PASTURE;
    };
    var number_token: sint;

    let SELF: tile = uninit;

    func init(hex: hex, kind: typeof(SELF.kind), number_token: sint) tile {
        return (:tile){
            .hex = hex,
            .kind = kind,
            .number_token = number_token,
        };
    }

    func resource(self: *tile) std::optional[[::resource]] {
        switch self.*.kind {
        tile::DESERT or tile::OCEAN {
            return std::optional[[::resource]]::EMPTY;
        }
        tile::FOREST {
            return std::optional[[::resource]]::init_value(::resource::WOOD);
        }
        tile::FIELD {
            return std::optional[[::resource]]::init_value(::resource::WHEAT);
        }
        tile::HILL {
            return std::optional[[::resource]]::init_value(::resource::BRICK);
        }
        tile::MOUNTAIN {
            return std::optional[[::resource]]::init_value(::resource::ORE);
        }
        tile::PASTURE {
            return std::optional[[::resource]]::init_value(::resource::SHEEP);
        }
        }

        std::unreachable(fileof(), lineof());
        return std::zeroed[[std::optional[[::resource]]]]();
    }

    func format(self: *tile, writer: std::writer, fmt: []byte) std::result[[void, std::error]] {
        var kind = "<error>";
        switch self.*.kind {
        tile::DESERT {
            kind = "desert";
        }
        tile::OCEAN {
            kind = "ocean";
        }
        tile::FOREST {
            kind = "forest";
        }
        tile::FIELD {
            kind = "field";
        }
        tile::HILL {
            kind = "hill";
        }
        tile::MOUNTAIN {
            kind = "mountain";
        }
        tile::PASTURE {
            kind = "pasture";
        }
        }

        if std::str::eq(fmt, "") {
            return std::write_all(writer, kind);
        }

        if std::str::eq(fmt, "verbose") {
            var resource = self.*.resource();
            if resource.is_empty() {
                return std::write_all(writer, kind);
            }

            var resource = resource.value();
            return std::write_format(
                writer,
                "{} ({}, {})",
                (:[]std::formatter)[
                    std::formatter::init[[[]byte]](&kind),
                    std::formatter::init[[::resource]](&resource),
                    std::formatter::init[[sint]](&self.*.number_token)]);
        }

        return std::result[[void, std::error]]::init_error(std::error::INVALID_ARGUMENT);
    }

    func init_from_bubby(value: *bubby::value) tile {
        var hex = hex::init_from_bubby(value.*.map_xlookup_string("hex"));
        var number = (:sint)value.*.map_xlookup_string("number").*.integer();
        var kind = value.*.map_xlookup_string("kind").*.string();
        if std::str::eq(kind, "desert") {
            return tile::init(hex, tile::DESERT, number);
        }
        if std::str::eq(kind, "ocean") {
            return tile::init(hex, tile::OCEAN, number);
        }
        if std::str::eq(kind, "forest") {
            return tile::init(hex, tile::FOREST, number);
        }
        if std::str::eq(kind, "field") {
            return tile::init(hex, tile::FIELD, number);
        }
        if std::str::eq(kind, "hill") {
            return tile::init(hex, tile::HILL, number);
        }
        if std::str::eq(kind, "mountain") {
            return tile::init(hex, tile::MOUNTAIN, number);
        }
        if std::str::eq(kind, "pasture") {
            return tile::init(hex, tile::PASTURE, number);
        }

        std::unreachable(fileof(), lineof());
        return std::zeroed[[tile]]();
    }

    func to_bubby(self: *tile) bubby::value {
        var out = bubby::value::init_map();
        out.map().*.insert(bubby::value::init_string("hex"), self.*.hex.to_bubby());
        switch self.*.kind {
        tile::DESERT {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("desert"));
        }
        tile::OCEAN {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("ocean"));
        }
        tile::FOREST {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("forest"));
        }
        tile::FIELD {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("field"));
        }
        tile::HILL {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("hill"));
        }
        tile::MOUNTAIN {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("mountain"));
        }
        tile::PASTURE {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("pasture"));
        }
        }
        out.map().*.insert(bubby::value::init_string("number"), bubby::value::init_integer((:s64)self.*.number_token));
        return out;
    }
}

struct edge {
    var hexes: [2]hex;

    func init(a: hex, b: hex) edge {
        assert hex::distance(a, b) == 1;

        var hexes = (:[2]hex)[a, b];
        std::sort[[hex]](hexes[0:countof(hexes)]);

        return (:edge){
            .hexes = hexes,
        };
    }

    func hash(self: *edge) usize {
        var hash = 0u;
        for i in countof(self.*.hexes) {
            hash = hash +% self.*.hexes[i].hash();
        }
        return hash;
    }

    func compare(lhs: *edge, rhs: *edge) ssize {
        var cmp = hex::compare(&lhs.*.hexes[0], &rhs.*.hexes[0]);
        if cmp != 0 {
            return cmp;
        }

        var cmp = hex::compare(&lhs.*.hexes[1], &rhs.*.hexes[1]);
        if cmp != 0 {
            return cmp;
        }

        return 0;
    }

    func format(self: *edge, writer: std::writer, fmt: []byte) std::result[[void, std::error]] {
        if std::str::eq(fmt, "") {
            for i in countof(self.*.hexes) {
                if i != 0 {
                    std::print(writer, ", ");
                }
                std::print_format(
                    writer,
                    "{}",
                    (:[]std::formatter)[
                        std::formatter::init[[hex]](&self.*.hexes[i])]);
            }

            return std::result[[void, std::error]]::init_value(void::VALUE);
        }

        return std::result[[void, std::error]]::init_error(std::error::INVALID_ARGUMENT);
    }

    func init_from_bubby(value: *bubby::value) edge {
        return edge::init(
            hex::init_from_bubby(&value.*.vector().*.data()[0]),
            hex::init_from_bubby(&value.*.vector().*.data()[1]));
    }

    func to_bubby(self: *edge) bubby::value {
        var out = bubby::value::init_vector();
        for i in countof(self.*.hexes) {
            out.vector().*.push(self.*.hexes[i].to_bubby());
        }
        return out;
    }
}

struct node {
    var hexes: [3]hex;

    func init(a: hex, b: hex, c: hex) node {
        assert hex::distance(a, b) == 1;
        assert hex::distance(b, c) == 1;
        assert hex::distance(c, a) == 1;

        var hexes = (:[3]hex)[a, b, c];
        std::sort[[hex]](hexes[0:countof(hexes)]);

        return (:node){
            .hexes = hexes,
        };
    }

    func hash(self: *node) usize {
        var hash = 0u;
        for i in countof(self.*.hexes) {
            hash = hash +% self.*.hexes[i].hash();
        }
        return hash;
    }

    func compare(lhs: *node, rhs: *node) ssize {
        var cmp = hex::compare(&lhs.*.hexes[0], &rhs.*.hexes[0]);
        if cmp != 0 {
            return cmp;
        }

        var cmp = hex::compare(&lhs.*.hexes[1], &rhs.*.hexes[1]);
        if cmp != 0 {
            return cmp;
        }

        var cmp = hex::compare(&lhs.*.hexes[2], &rhs.*.hexes[2]);
        if cmp != 0 {
            return cmp;
        }

        return 0;
    }

    func format(self: *node, writer: std::writer, fmt: []byte) std::result[[void, std::error]] {
        if std::str::eq(fmt, "") {
            for i in countof(self.*.hexes) {
                if i != 0 {
                    std::print(writer, ", ");
                }
                std::print_format(
                    writer,
                    "{}",
                    (:[]std::formatter)[
                        std::formatter::init[[hex]](&self.*.hexes[i])]);
            }

            return std::result[[void, std::error]]::init_value(void::VALUE);
        }

        return std::result[[void, std::error]]::init_error(std::error::INVALID_ARGUMENT);
    }

    func init_from_bubby(value: *bubby::value) node {
        return node::init(
            hex::init_from_bubby(&value.*.vector().*.data()[0]),
            hex::init_from_bubby(&value.*.vector().*.data()[1]),
            hex::init_from_bubby(&value.*.vector().*.data()[2]));
    }

    func to_bubby(self: *node) bubby::value {
        var out = bubby::value::init_vector();
        for i in countof(self.*.hexes) {
            out.vector().*.push(self.*.hexes[i].to_bubby());
        }
        return out;
    }
}

struct port {
    var hex: hex;
    var nodes: [2]node;
    var kind: enum {
        ANY;
        BRICK;
        ORE;
        SHEEP;
        WHEAT;
        WOOD;
    };

    let SELF: port = uninit;

    func init(hex: hex, a: node, b: node, kind: typeof(SELF.kind)) port {
        assert std::eq[[::hex]](&hex, &a.hexes[0])
            or std::eq[[::hex]](&hex, &a.hexes[1])
            or std::eq[[::hex]](&hex, &a.hexes[2]);
        assert std::eq[[::hex]](&hex, &b.hexes[0])
            or std::eq[[::hex]](&hex, &b.hexes[1])
            or std::eq[[::hex]](&hex, &b.hexes[2]);
        assert std::ne[[node]](&a, &b);

        var nodes = (:[2]node)[a, b];
        std::sort[[node]](nodes[0:countof(nodes)]);

        return (:port){
            .hex = hex,
            .nodes = nodes,
            .kind = kind,
        };
    }

    func hash(self: *port) usize {
        return self.*.hex.hash();
    }

    func compare(lhs: *port, rhs: *port) ssize {
        return std::compare[[hex]](&lhs.*.hex, &rhs.*.hex);
    }

    func init_from_bubby(value: *bubby::value) port {
        var hex = hex::init_from_bubby(value.*.map_xlookup_string("hex"));
        var node_a = node::init_from_bubby(&value.*.map_xlookup_string("nodes").*.vector().*.data()[0]);
        var node_b = node::init_from_bubby(&value.*.map_xlookup_string("nodes").*.vector().*.data()[1]);
        var kind = value.*.map_xlookup_string("kind").*.string();
        if std::str::eq(kind, "any") {
            return port::init(hex, node_a, node_b, port::ANY);
        }
        if std::str::eq(kind, "brick") {
            return port::init(hex, node_a, node_b, port::BRICK);
        }
        if std::str::eq(kind, "ore") {
            return port::init(hex, node_a, node_b, port::ORE);
        }
        if std::str::eq(kind, "sheep") {
            return port::init(hex, node_a, node_b, port::SHEEP);
        }
        if std::str::eq(kind, "wheat") {
            return port::init(hex, node_a, node_b, port::WHEAT);
        }
        if std::str::eq(kind, "wood") {
            return port::init(hex, node_a, node_b, port::WOOD);
        }

        std::unreachable(fileof(), lineof());
        return std::zeroed[[port]]();
    }

    func to_bubby(self: *port) bubby::value {
        var out = bubby::value::init_map();
        out.map().*.insert(bubby::value::init_string("hex"), self.*.hex.to_bubby());
        out.map().*.insert(bubby::value::init_string("nodes"), bubby::value::init_vector());
        out.map_xlookup_string("nodes").*.vector().*.push(self.*.nodes[0].to_bubby());
        out.map_xlookup_string("nodes").*.vector().*.push(self.*.nodes[1].to_bubby());
        switch self.*.kind {
        port::ANY {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("any"));
        }
        port::BRICK {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("brick"));
        }
        port::ORE {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("ore"));
        }
        port::SHEEP {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("sheep"));
        }
        port::WHEAT {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("wheat"));
        }
        port::WOOD {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("wood"));
        }
        }
        return out;
    }
}

struct road {
    var player: player;
    var edge: edge;

    func init(player: player, edge: edge) road {
        return (:road){
            .edge = edge,
            .player = player,
        };
    }

    func hash(self: *road) usize {
        return self.*.edge.hash();
    }

    func compare(lhs: *road, rhs: *road) ssize {
        return std::compare[[edge]](&lhs.*.edge, &rhs.*.edge);
    }

    func init_from_bubby(value: *bubby::value) road {
        return road::init(
            (:player)value.*.map_xlookup_string("player").*.integer(),
            edge::init_from_bubby(value.*.map_xlookup_string("edge")));
    }

    func to_bubby(self: *road) bubby::value {
        var out = bubby::value::init_map();
        out.map_insert_string("player", bubby::value::init_integer((:s64)self.*.player));
        out.map_insert_string("edge", self.*.edge.to_bubby());
        return out;
    }
}

struct town {
    var player: player;
    var node: node;
    var kind: enum {
        SETTLEMENT;
        CITY;
    };

    let SELF: town = uninit;

    func init(player: player, node: node, kind: typeof(SELF.kind)) town {
        return (:town){
            .player = player,
            .node = node,
            .kind = kind,
        };
    }

    func hash(self: *town) usize {
        return self.*.node.hash();
    }

    func compare(lhs: *town, rhs: *town) ssize {
        return std::compare[[node]](&lhs.*.node, &rhs.*.node);
    }

    func init_from_bubby(value: *bubby::value) town {
        var player = (:player)value.*.map_xlookup_string("player").*.integer();
        var node = node::init_from_bubby(value.*.map_xlookup_string("node"));
        var kind = value.*.map_xlookup_string("kind").*.string();
        if std::str::eq(kind, "settlement") {
            return town::init(player, node, town::SETTLEMENT);
        }
        if std::str::eq(kind, "city") {
            return town::init(player, node, town::CITY);
        }

        std::unreachable(fileof(), lineof());
        return std::zeroed[[town]]();
    }

    func to_bubby(self: *town) bubby::value {
        var out = bubby::value::init_map();
        out.map_insert_string("player", bubby::value::init_integer((:s64)self.*.player));
        out.map_insert_string("node", self.*.node.to_bubby());
        switch self.*.kind {
        town::SETTLEMENT {
            out.map_insert_string("kind", bubby::value::init_string("settlement"));
        }
        town::CITY {
            out.map_insert_string("kind", bubby::value::init_string("city"));
        }
        }
        return out;
    }
}

struct board {
    var tiles: std::hash_map[[hex, tile]];
    var edges: std::hash_set[[edge]];
    var nodes: std::hash_set[[node]];
    var ports: std::hash_set[[port]];
    var roads: std::hash_map[[edge, road]];
    var towns: std::hash_map[[node, town]];
    var robber: hex;

    func init() board {
        var tiles = std::hash_map[[hex, tile]]::init();
        var edges = std::hash_set[[edge]]::init();
        var nodes = std::hash_set[[node]]::init();
        var ports = std::hash_set[[port]]::init();
        var roads = std::hash_map[[edge, road]]::init();
        var towns = std::hash_map[[node, town]]::init();
        var robber = hex::init(+0, +0, +0);

        return (:board){
            .tiles = tiles,
            .edges = edges,
            .nodes = nodes,
            .ports = ports,
            .roads = roads,
            .towns = towns,
            .robber = robber,
        };
    }

    func init_assign(from: *board) board {
        var self = board::init();
        board::assign(&self, from);
        return self;
    }

    func init_starter_map_for_beginners() board {
        var tiles = init_starter_map_for_beginners_tiles();
        var edges = init_edges(&tiles);
        var nodes = init_nodes(&tiles);
        var ports = init_starter_map_for_beginners_ports();
        var roads = init_starter_map_for_beginners_roads();
        var towns = init_starter_map_for_beginners_towns();

        var robber = hex::init(+0, +0, +0);

        return (:board){
            .tiles = tiles,
            .edges = edges,
            .nodes = nodes,
            .ports = ports,
            .roads = roads,
            .towns = towns,
            .robber = robber,
        };
    }

    func fini(self: *board) void {
        self.*.tiles.fini();
        self.*.edges.fini();
        self.*.nodes.fini();
        self.*.ports.fini();
        self.*.roads.fini();
        self.*.towns.fini();
    }

    func assign(self: *board, from: *board) void {
        std::hash_map[[hex, tile]]::assign(&self.*.tiles, &from.*.tiles);
        std::hash_set[[edge]]::assign(&self.*.edges, &from.*.edges);
        std::hash_set[[node]]::assign(&self.*.nodes, &from.*.nodes);
        std::hash_set[[port]]::assign(&self.*.ports, &from.*.ports);
        std::hash_map[[edge, road]]::assign(&self.*.roads, &from.*.roads);
        std::hash_map[[node, town]]::assign(&self.*.towns, &from.*.towns);
        self.*.robber = from.*.robber;
    }

    func init_from_bubby(value: *bubby::value) board {
        var self = board::init();
        var tiles = value.*.map_xlookup_string("tiles");
        var iter = std::hash_set_iterator[[bubby::value]]::init(tiles.*.set());
        for iter.advance() {
            var tile = tile::init_from_bubby(iter.current());
            self.tiles.insert(tile.hex, tile);
        }
        self.edges.fini();
        self.edges = init_edges(&self.tiles);
        self.nodes.fini();
        self.nodes = init_nodes(&self.tiles);
        var ports = value.*.map_xlookup_string("ports");
        var iter = std::hash_set_iterator[[bubby::value]]::init(ports.*.set());
        for iter.advance() {
            var port = port::init_from_bubby(iter.current());
            self.ports.insert(port);
        }
        var roads = value.*.map_xlookup_string("roads");
        var iter = std::hash_set_iterator[[bubby::value]]::init(roads.*.set());
        for iter.advance() {
            var road = road::init_from_bubby(iter.current());
            self.roads.insert(road.edge, road);
        }
        var towns = value.*.map_xlookup_string("towns");
        var iter = std::hash_set_iterator[[bubby::value]]::init(towns.*.set());
        for iter.advance() {
            var town = town::init_from_bubby(iter.current());
            self.towns.insert(town.node, town);
        }
        self.robber = hex::init_from_bubby(value.*.map_xlookup_string("robber"));
        return self;
    }

    func to_bubby(self: *board) bubby::value {
        var out = bubby::value::init_map();
        out.map_insert_string("tiles", bubby::value::init_set());
        var iter = std::hash_map_iterator[[hex, tile]]::init(&self.*.tiles);
        for iter.advance() {
            var tile = iter.current().*.value.*.to_bubby();
            out.map_xlookup_string("tiles").*.set_insert(tile);
        }
        out.map_insert_string("ports", bubby::value::init_set());
        var iter = std::hash_set_iterator[[port]]::init(&self.*.ports);
        for iter.advance() {
            var port = iter.current().*.to_bubby();
            out.map_xlookup_string("ports").*.set_insert(port);
        }
        out.map_insert_string("roads", bubby::value::init_set());
        var iter = std::hash_map_iterator[[edge, road]]::init(&self.*.roads);
        for iter.advance() {
            var road = iter.current().*.value.*.to_bubby();
            out.map_xlookup_string("roads").*.set_insert(road);
        }
        out.map_insert_string("towns", bubby::value::init_set());
        var iter = std::hash_map_iterator[[node, town]]::init(&self.*.towns);
        for iter.advance() {
            var town = iter.current().*.value.*.to_bubby();
            out.map_xlookup_string("towns").*.set_insert(town);
        }
        out.map_insert_string("robber", self.*.robber.to_bubby());
        return out;
    }
}

func insert_tile(tiles: *std::hash_map[[hex, tile]], tile: tile) void {
    tiles.*.insert(tile.hex, tile);
}

func insert_road(roads: *std::hash_map[[edge, road]], road: road) void {
    roads.*.insert(road.edge, road);
}

func insert_town(towns: *std::hash_map[[node, town]], town: town) void {
    towns.*.insert(town.node, town);
}

func init_starter_map_for_beginners_tiles() std::hash_map[[hex, tile]] {
    var tiles = std::hash_map[[hex, tile]]::init();

    # top row
    insert_tile(&tiles, tile::init(hex::init(+0, -2, +2), tile::MOUNTAIN, 10));
    insert_tile(&tiles, tile::init(hex::init(+1, -2, +1), tile::PASTURE, 2));
    insert_tile(&tiles, tile::init(hex::init(+2, -2, +0), tile::FOREST, 9));
    # top-middle row
    insert_tile(&tiles, tile::init(hex::init(-1, -1, +2), tile::FIELD, 12));
    insert_tile(&tiles, tile::init(hex::init(+0, -1, +1), tile::HILL, 6));
    insert_tile(&tiles, tile::init(hex::init(+1, -1, +0), tile::PASTURE, 4));
    insert_tile(&tiles, tile::init(hex::init(+2, -1, -1), tile::HILL, 10));
    # middle row
    insert_tile(&tiles, tile::init(hex::init(-2, +0, +2), tile::FIELD, 9));
    insert_tile(&tiles, tile::init(hex::init(-1, +0, +1), tile::FOREST, 11));
    insert_tile(&tiles, tile::init(hex::init(+0, +0, +0), tile::DESERT, -1));
    insert_tile(&tiles, tile::init(hex::init(+1, +0, -1), tile::FOREST, 3));
    insert_tile(&tiles, tile::init(hex::init(+2, +0, -2), tile::MOUNTAIN, 8));
    # bottom-middle row
    insert_tile(&tiles, tile::init(hex::init(-2, +1, +1), tile::FOREST, 8));
    insert_tile(&tiles, tile::init(hex::init(-1, +1, +0), tile::MOUNTAIN, 3));
    insert_tile(&tiles, tile::init(hex::init(+0, +1, -1), tile::FIELD, 4));
    insert_tile(&tiles, tile::init(hex::init(+1, +1, -2), tile::PASTURE, 5));
    # bottom row
    insert_tile(&tiles, tile::init(hex::init(-2, +2, +0), tile::HILL, 5));
    insert_tile(&tiles, tile::init(hex::init(-1, +2, -1), tile::FIELD, 6));
    insert_tile(&tiles, tile::init(hex::init(+0, +2, -2), tile::PASTURE, 11));
    # adjacent ocean
    insert_tile(&tiles, tile::init(hex::init(+0, -3, +3), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(+1, -3, +2), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(+2, -3, +1), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(+3, -3, +0), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(-1, -2, +3), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(+3, -2, -1), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(-2, -1, +3), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(+3, -1, -2), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(-3, +0, +3), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(+3, +0, -3), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(-3, +1, +2), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(+2, +1, -3), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(-3, +2, +1), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(+1, +2, -3), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(-3, +3, +0), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(-2, +3, -1), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(-1, +3, -2), tile::OCEAN, -1));
    insert_tile(&tiles, tile::init(hex::init(+0, +3, -3), tile::OCEAN, -1));

    return tiles;
}

func init_edges(tiles: *std::hash_map[[hex, tile]]) std::hash_set[[edge]] {
    var edges = std::hash_set[[edge]]::init();

    var iter_i = std::hash_map_iterator[[hex, tile]]::init(tiles);
    for iter_i.advance() {
        var iter_j = std::hash_map_iterator[[hex, tile]]::init(tiles);
        for iter_j.advance() {
            if hex::distance(*iter_i.current().*.key, *iter_j.current().*.key) != 1 {
                continue;
            }
            var edge = edge::init(
                *iter_i.current().*.key,
                *iter_j.current().*.key);
            edges.insert(edge);
        }
    }

    return edges;
}

func init_nodes(tiles: *std::hash_map[[hex, tile]]) std::hash_set[[node]] {
    var nodes = std::hash_set[[node]]::init();

    var iter_i = std::hash_map_iterator[[hex, tile]]::init(tiles);
    for iter_i.advance() {
        var iter_j = std::hash_map_iterator[[hex, tile]]::init(tiles);
        for iter_j.advance() {
            var iter_k = std::hash_map_iterator[[hex, tile]]::init(tiles);
            for iter_k.advance() {
                if hex::distance(*iter_i.current().*.key, *iter_j.current().*.key) != 1 {
                    continue;
                }
                if hex::distance(*iter_j.current().*.key, *iter_k.current().*.key) != 1 {
                    continue;
                }
                if hex::distance(*iter_k.current().*.key, *iter_i.current().*.key) != 1 {
                    continue;
                }
                var node = node::init(
                    *iter_i.current().*.key,
                    *iter_j.current().*.key,
                    *iter_k.current().*.key);
                nodes.insert(node);
            }
        }
    }

    return nodes;
}

func init_starter_map_for_beginners_ports() std::hash_set[[port]] {
    var ports = std::hash_set[[port]]::init();

    ports.insert(port::init(
        hex::init(+3, +0, -3),
        node::init(
            hex::init(+2, +0, -2),
            hex::init(+2, +1, -3),
            hex::init(+3, +0, -3)),
        node::init(
            hex::init(+2, +0, -2),
            hex::init(+3, -1, -2),
            hex::init(+3, +0, -3)),
        port::ANY));

    ports.insert(port::init(
        hex::init(+3, -2, -1),
        node::init(
            hex::init(+2, -2, +0),
            hex::init(+2, -1, -1),
            hex::init(+3, -2, -1)),
        node::init(
            hex::init(+2, -1, -1),
            hex::init(+3, -2, -1),
            hex::init(+3, -1, -2)),
        port::ORE));

    ports.insert(port::init(
        hex::init(+2, -3, +1),
        node::init(
            hex::init(+1, -3, +2),
            hex::init(+1, -2, +1),
            hex::init(+2, -3, +1)),
        node::init(
            hex::init(+1, -2, +1),
            hex::init(+2, -3, +1),
            hex::init(+2, -2, +0)),
        port::WHEAT));

    ports.insert(port::init(
        hex::init(+0, -3, +3),
        node::init(
            hex::init(-1, -2, +3),
            hex::init(+0, -3, +3),
            hex::init(+0, -2, +2)),
        node::init(
            hex::init(+0, -3, +3),
            hex::init(+0, -2, +2),
            hex::init(+1, -3, +2)),
        port::ANY));

    ports.insert(port::init(
        hex::init(-2, -1, +3),
        node::init(
            hex::init(-2, -1, +3),
            hex::init(-2, +0, +2),
            hex::init(-1, -1, +2)),
        node::init(
            hex::init(-2, -1, +3),
            hex::init(-1, -2, +3),
            hex::init(-1, -1, +2)),
        port::WOOD));

    ports.insert(port::init(
        hex::init(-3, +1, +2),
        node::init(
            hex::init(-3, +1, +2),
            hex::init(-3, +2, +1),
            hex::init(-2, +1, +1)),
        node::init(
            hex::init(-3, +1, +2),
            hex::init(-2, +0, +2),
            hex::init(-2, +1, +1)),
        port::BRICK));

    ports.insert(port::init(
        hex::init(-3, +3, +0),
        node::init(
            hex::init(-3, +2, +1),
            hex::init(-3, +3, +0),
            hex::init(-2, +2, +0)),
        node::init(
            hex::init(-3, +3, +0),
            hex::init(-2, +2, +0),
            hex::init(-2, +3, -1)),
        port::ANY));

    ports.insert(port::init(
        hex::init(-1, +3, -2),
        node::init(
            hex::init(-2, +3, -1),
            hex::init(-1, +2, -1),
            hex::init(-1, +3, -2)),
        node::init(
            hex::init(-1, +2, -1),
            hex::init(-1, +3, -2),
            hex::init(+0, +2, -2)),
        port::ANY));

    ports.insert(port::init(
        hex::init(+1, +2, -3),
        node::init(
            hex::init(+0, +2, -2),
            hex::init(+1, +1, -2),
            hex::init(+1, +2, -3)),
        node::init(
            hex::init(+1, +1, -2),
            hex::init(+1, +2, -3),
            hex::init(+2, +1, -3)),
        port::SHEEP));

    return ports;
}

func init_starter_map_for_beginners_roads() std::hash_map[[edge, road]] {
    var roads = std::hash_map[[edge, road]]::init();

    # Red starting position.
    insert_road(&roads, road::init(
        player::RED,
        edge::init(
            hex::init(-2, +1, +1),
            hex::init(-1, +0, +1))));
    insert_road(&roads, road::init(
        player::RED,
        edge::init(
            hex::init(+0, -1, +1),
            hex::init(+1, -2, +1))));

    # Blue starting position.
    insert_road(&roads, road::init(
        player::BLUE,
        edge::init(
            hex::init(-2, +2, +0),
            hex::init(-1, +1, +0))));
    insert_road(&roads, road::init(
        player::BLUE,
        edge::init(
            hex::init(+0, +1, -1),
            hex::init(+1, +1, -2))));

    # White starting position.
    insert_road(&roads, road::init(
        player::WHITE,
        edge::init(
            hex::init(-1, -1, +2),
            hex::init(-1, +0, +1))));
    insert_road(&roads, road::init(
        player::WHITE,
        edge::init(
            hex::init(+1, +0, -1),
            hex::init(+2, +0, -2))));

    # Orange starting position.
    insert_road(&roads, road::init(
        player::ORANGE,
        edge::init(
            hex::init(-1, +2, -1),
            hex::init(+0, +1, -1))));
    insert_road(&roads, road::init(
        player::ORANGE,
        edge::init(
            hex::init(+1, -1, +0),
            hex::init(+2, -2, +0))));

    return roads;
}

func init_starter_map_for_beginners_towns() std::hash_map[[node, town]] {
    var towns = std::hash_map[[node, town]]::init();

    # Red starting position.
    insert_town(&towns, town::init(
        player::RED,
        node::init(
            hex::init(-2, +0, +2),
            hex::init(-2, +1, +1),
            hex::init(-1, +0, +1)),
        town::SETTLEMENT));
    insert_town(&towns, town::init(
        player::RED,
        node::init(
            hex::init(+0, -2, +2),
            hex::init(+0, -1, +1),
            hex::init(+1, -2, +1)),
        town::SETTLEMENT));

    # Blue starting position.
    insert_town(&towns, town::init(
        player::BLUE,
        node::init(
            hex::init(-2, +1, +1),
            hex::init(-2, +2, +0),
            hex::init(-1, +1, +0)),
        town::SETTLEMENT));
    insert_town(&towns, town::init(
        player::BLUE,
        node::init(
            hex::init(+0, +1, -1),
            hex::init(+0, +2, -2),
            hex::init(+1, +1, -2)),
        town::SETTLEMENT));

    # White starting position.
    insert_town(&towns, town::init(
        player::WHITE,
        node::init(
            hex::init(-1, -1, +2),
            hex::init(-1, +0, +1),
            hex::init(+0, -1, +1)),
        town::SETTLEMENT));
    insert_town(&towns, town::init(
        player::WHITE,
        node::init(
            hex::init(+1, +0, -1),
            hex::init(+1, +1, -2),
            hex::init(+2, +0, -2)),
        town::SETTLEMENT));

    # Orange starting position.
    insert_town(&towns, town::init(
        player::ORANGE,
        node::init(
            hex::init(-1, +1, +0),
            hex::init(-1, +2, -1),
            hex::init(+0, +1, -1)),
        town::SETTLEMENT));
    insert_town(&towns, town::init(
        player::ORANGE,
        node::init(
            hex::init(+1, -1, +0),
            hex::init(+2, -2, +0),
            hex::init(+2, -1, -1)),
        town::SETTLEMENT));

    return towns;
}

func adjacent_edges_to_edge(target: edge, edges: *std::hash_set[[edge]], nodes: *std::hash_set[[node]]) std::hash_set[[edge]] {
    var out = std::hash_set[[edge]]::init();

    var adjacent_nodes = adjacent_nodes_to_edge(target, nodes);
    defer adjacent_nodes.fini();

    var iter = std::hash_set_iterator[[node]]::init(&adjacent_nodes);
    for iter.advance() {
        var adjacent_edges = adjacent_edges_to_node(*iter.current(), edges);
        defer adjacent_edges.fini();

        var iter = std::hash_set_iterator[[edge]]::init(&adjacent_edges);
        for iter.advance() {
            if not std::eq[[edge]](&target, iter.current()) {
                out.insert(*iter.current());
            }
        }
    }

    return out;
}

func adjacent_nodes_to_edge(target: edge, nodes: *std::hash_set[[node]]) std::hash_set[[node]] {
    var out = std::hash_set[[node]]::init();

    var a = std::hash_set[[hex]]::init();
    defer a.fini();
    a.insert(target.hexes[0]);
    a.insert(target.hexes[1]);

    var iter = std::hash_set_iterator[[node]]::init(nodes);
    for iter.advance() {
        var b = std::hash_set[[hex]]::init();
        defer b.fini();
        b.insert(iter.current().*.hexes[0]);
        b.insert(iter.current().*.hexes[1]);
        b.insert(iter.current().*.hexes[2]);

        var intersection = std::hash_set[[hex]]::init_intersection(&a, &b);
        defer intersection.fini();
        if intersection.count() == 2 {
            out.insert(*iter.current());
        }
    }

    return out;
}

func adjacent_edges_to_node(target: node, edges: *std::hash_set[[edge]]) std::hash_set[[edge]] {
    var out = std::hash_set[[edge]]::init();

    var a = std::hash_set[[hex]]::init();
    defer a.fini();
    a.insert(target.hexes[0]);
    a.insert(target.hexes[1]);
    a.insert(target.hexes[2]);

    var iter = std::hash_set_iterator[[edge]]::init(edges);
    for iter.advance() {
        var b = std::hash_set[[hex]]::init();
        defer b.fini();
        b.insert(iter.current().*.hexes[0]);
        b.insert(iter.current().*.hexes[1]);

        var intersection = std::hash_set[[hex]]::init_intersection(&a, &b);
        defer intersection.fini();
        if intersection.count() == 2 {
            out.insert(*iter.current());
        }
    }

    return out;
}

func adjacent_nodes_to_node(target: node, nodes: *std::hash_set[[node]]) std::hash_set[[node]] {
    var out = std::hash_set[[node]]::init();

    var a = std::hash_set[[hex]]::init();
    defer a.fini();
    a.insert(target.hexes[0]);
    a.insert(target.hexes[1]);
    a.insert(target.hexes[2]);

    var iter = std::hash_set_iterator[[node]]::init(nodes);
    for iter.advance() {
        var b = std::hash_set[[hex]]::init();
        defer b.fini();
        b.insert(iter.current().*.hexes[0]);
        b.insert(iter.current().*.hexes[1]);
        b.insert(iter.current().*.hexes[2]);

        var intersection = std::hash_set[[hex]]::init_intersection(&a, &b);
        defer intersection.fini();
        if intersection.count() == 2 {
            out.insert(*iter.current());
        }
    }

    return out;
}

let BUSY_CODE = (:sint)42; # nbnet busy code

struct message {
    let TYPE: u8 = 0;
    let SIZE: usize = 4096 * 4;

    var len: uint;
    var buf: [SIZE]char;

    func create() *message {
        return std::new[[message]]();
    }

    func destroy(self: *message) void {
        std::delete[[message]](self);
    }

    func serialize(self: *message, stream: *NBN_Stream) sint {
        stream.*.serialize_uint_func(stream, &self.*.len, 0, (:uint)SIZE);
        stream.*.serialize_bytes_func(stream, (:*u8)&self.*.buf[0], self.*.len);
        return 0;
    }
}

struct client_message {
    var kind: enum {
        BUILD_ROAD;
        BUILD_TOWN;
        REMOVE_ROAD;
        REMOVE_TOWN;
        MOVE_ROBBER;
    };
    var data: union {
        var build_road: road;
        var build_town: town;
        var remove_road: edge;
        var remove_town: node;
        var move_robber: hex;
    };

    let SELF: client_message = uninit;

    func init_build_road(road: road) client_message {
        return (:client_message){
            .kind = client_message::BUILD_ROAD,
            .data = (:typeof(SELF.data)){.build_road = road},
        };
    }

    func init_build_town(town: town) client_message {
        return (:client_message){
            .kind = client_message::BUILD_TOWN,
            .data = (:typeof(SELF.data)){.build_town = town},
        };
    }

    func init_remove_road(road: edge) client_message {
        return (:client_message){
            .kind = client_message::REMOVE_ROAD,
            .data = (:typeof(SELF.data)){.remove_road = road},
        };
    }

    func init_remove_town(town: node) client_message {
        return (:client_message){
            .kind = client_message::REMOVE_TOWN,
            .data = (:typeof(SELF.data)){.remove_town = town},
        };
    }

    func init_move_robber(hex: hex) client_message {
        return (:client_message){
            .kind = client_message::MOVE_ROBBER,
            .data = (:typeof(SELF.data)){.move_robber = hex},
        };
    }

    func init_from_bubby(value: *bubby::value) client_message {
        var kind = value.*.map_xlookup_string("kind");
        if std::str::eq(kind.*.string(), "build-road") {
            var road = road::init_from_bubby(value.*.map_xlookup_string("data"));
            return client_message::init_build_road(road);
        }
        if std::str::eq(kind.*.string(), "build-town") {
            var town = town::init_from_bubby(value.*.map_xlookup_string("data"));
            return client_message::init_build_town(town);
        }
        if std::str::eq(kind.*.string(), "remove-road") {
            var edge = edge::init_from_bubby(value.*.map_xlookup_string("data"));
            return client_message::init_remove_road(edge);
        }
        if std::str::eq(kind.*.string(), "remove-town") {
            var node = node::init_from_bubby(value.*.map_xlookup_string("data"));
            return client_message::init_remove_town(node);
        }
        if std::str::eq(kind.*.string(), "move-robber") {
            var hex = hex::init_from_bubby(value.*.map_xlookup_string("data"));
            return client_message::init_move_robber(hex);
        }

        std::unreachable(fileof(), lineof());
        return std::zeroed[[client_message]]();
    }

    func to_bubby(self: *client_message) bubby::value {
        var out = bubby::value::init_map();
        switch self.*.kind {
        client_message::BUILD_ROAD {
            out.map_insert_string("kind", bubby::value::init_string("build-road"));
            out.map_insert_string("data", self.*.data.build_road.to_bubby());
        }
        client_message::BUILD_TOWN {
            out.map_insert_string("kind", bubby::value::init_string("build-town"));
            out.map_insert_string("data", self.*.data.build_town.to_bubby());
        }
        client_message::REMOVE_ROAD {
            out.map_insert_string("kind", bubby::value::init_string("remove-road"));
            out.map_insert_string("data", self.*.data.remove_road.to_bubby());
        }
        client_message::REMOVE_TOWN {
            out.map_insert_string("kind", bubby::value::init_string("remove-town"));
            out.map_insert_string("data", self.*.data.remove_town.to_bubby());
        }
        client_message::MOVE_ROBBER {
            out.map_insert_string("kind", bubby::value::init_string("move-robber"));
            out.map_insert_string("data", self.*.data.move_robber.to_bubby());
        }
        }
        return out;
    }
}

struct server_message {
    var kind: enum {
        BOARD;
        ACCEPTED;
        REJECTED;
    };
    var data: union {
        var board: board;
        var accepted: std::string;
        var rejected: std::string;
    };

    let SELF: server_message = uninit;

    func init_board(board: *board) server_message {
        return (:server_message){
            .kind = server_message::BOARD,
            .data = (:typeof(SELF.data)){.board = ::board::init_assign(board)},
        };
    }

    func init_accepted(text: []byte) server_message {
        return (:server_message){
            .kind = server_message::ACCEPTED,
            .data = (:typeof(SELF.data)){.accepted = std::string::init_from_str(text)},
        };
    }

    func init_rejected(text: []byte) server_message {
        return (:server_message){
            .kind = server_message::REJECTED,
            .data = (:typeof(SELF.data)){.rejected = std::string::init_from_str(text)},
        };
    }

    func fini(self: *server_message) void {
        switch self.*.kind {
        server_message::BOARD {
            self.*.data.board.fini();
        }
        server_message::ACCEPTED {
            self.*.data.accepted.fini();
        }
        server_message::REJECTED {
            self.*.data.rejected.fini();
        }
        }
    }

    func init_from_bubby(value: *bubby::value) server_message {
        var kind = value.*.map_xlookup_string("kind").*.string();
        if std::str::eq(kind, "board") {
            var board = board::init_from_bubby(value.*.map_xlookup_string("data"));
            defer board.fini();
            return server_message::init_board(&board);
        }
        if std::str::eq(kind, "accepted") {
            return server_message::init_accepted(value.*.map_xlookup_string("data").*.string());
        }
        if std::str::eq(kind, "rejected") {
            return server_message::init_rejected(value.*.map_xlookup_string("data").*.string());
        }

        std::unreachable(fileof(), lineof());
        return std::zeroed[[server_message]]();
    }

    func to_bubby(self: *server_message) bubby::value {
        var out = bubby::value::init_map();
        switch self.*.kind {
        server_message::BOARD {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("board"));
            out.map().*.insert(bubby::value::init_string("data"), self.*.data.board.to_bubby());
        }
        server_message::ACCEPTED {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("accepted"));
            out.map().*.insert(bubby::value::init_string("data"), bubby::value::init_string(self.*.data.accepted.data()));
        }
        server_message::REJECTED {
            out.map().*.insert(bubby::value::init_string("kind"), bubby::value::init_string("rejected"));
            out.map().*.insert(bubby::value::init_string("data"), bubby::value::init_string(self.*.data.rejected.data()));
        }
        }
        return out;
    }
}

struct message_sender_itable[[MESSAGE]] {
    var send: func(*any, *MESSAGE) void;
}

struct message_sender[[MESSAGE]] {
    var itable: *message_sender_itable[[MESSAGE]];
    var object: *any;

    func init[[T]](object: *T) message_sender[[MESSAGE]] {
        let itable = (:message_sender_itable[[MESSAGE]]){
            .send = T::send,
        };
        return (:message_sender[[MESSAGE]]){
            .itable = &itable,
            .object = object,
        };
    }

    func send(self: *message_sender[[MESSAGE]], message: *MESSAGE) void {
        self.*.itable.*.send(self.*.object, message);
    }
}
